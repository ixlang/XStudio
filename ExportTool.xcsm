//xlang Source, Name:ExportTool.xcsm 
//Date: Fri Dec 14:16:12 2018 

class ExportTool : QDialog{

	QLineEdit edtStaticLib ,destfile, edtName, edtVersion, lelDescr, lblAuthor;
    QComboBox cbbNative, cbbOs;
    QPushButton stBrowser, btnEmpty, btnAdd , brwExport, btnExport, btnReadme, btnIcon, btnsave, btnload;
    QTreeWidget  structView;
    QProgressBar	prgExport;
    
    
    public class FilePackage{
		public int id;
		public Vector<String> files = new Vector<String>();
        
        public FilePackage(int i){
			id = i;
        }
        
        public void preview(@NotNilptr QTreeWidget tree, long item){
        
			String [] oss = {"Windows", "Linux", "Macos", "其他"};
			long hroot = tree.insertItem(item, "res/toolbar/winoverlap.png", oss[id]);
            
            for (int i = 0, c = files.size(); i < c; i++){
				tree.insertItem(hroot, "res/toolbar/source.png", files.get(i));
            }
            
        }
        
        public bool build(@NotNilptr ZipArchive zs,@NotNilptr  String path){
			String myPath = path.appendPath("" + id);
			//zs.createEntry(new ZipFile(myPath, nilptr, true, _system_.currentTimeMillis()));
            
            for (int i = 0, c = files.size(); i < c; i++){
				String filename = files.get(i);
                if (filename != nilptr){
                    if (false == zs.createEntry(new ZipFile(myPath.appendPath(filename.findFilenameAndExtension()), new FileInputStream(filename), false, _system_.currentTimeMillis()))){
                        QMessageBox.Critical("错误", "添加文件 " + filename + " 时发生错误", QMessageBox.Ok, QMessageBox.Ok);
                        return false;
                    }
                }
            }
            return true;
        }
        
        public JsonArray save(){
			JsonArray filesobj = new JsonArray();
            for (int i = 0, c = files.size(); i < c; i++){
				filesobj.put(files.get(i));
            }
            return filesobj;
        }
        
        public void load(String file){
			files.add(file);
        }
    };
    
    public class Arch{
		public int id;
        public Map<int, FilePackage> osFile = new Map<int, FilePackage>();
        
        public Arch(int ai){
			id = ai;
            for (int i =0; i < 4; i ++){
				osFile.put(i,new FilePackage(i));
            }
        }
        
        public @NotNilptr Map<int, FilePackage> getOsFile(){
            return osFile;
        }
        
        public void preview(@NotNilptr QTreeWidget tree, long item){
			String [] options = {"default", "x86", "x86_64", "arm", "arm64", "mips"};
			long hroot = tree.insertItem(item, "res/toolbar/console.png", options[id]);
            Map.Iterator<int, FilePackage> iters = osFile.iterator();
            
            while (iters.hasNext()){
                FilePackage fp = iters.getValue();
                if (fp != nilptr){
                    fp.preview(tree, hroot);
                }
				iters.next();
            }            
        }
        
        public bool build(@NotNilptr ZipArchive zs,@NotNilptr  String path){
			String myPath = path.appendPath("" + id);
			//zs.createEntry(new ZipFile(myPath, nilptr, true, _system_.currentTimeMillis()));
            
            Map.Iterator<int, FilePackage> iters = osFile.iterator();
            
            while (iters.hasNext()){
                FilePackage fp = iters.getValue();
                if (fp != nilptr){
                    if (fp.build(zs, myPath) == false){
                        return false;
                    }
                    
                    prgExport.setValue((id + 1) * 20 + (fp.id * 5));
                }
                iters.next();
            }     
            return true;
        }
        
        public JsonObject save(){
			JsonObject object = new JsonObject();
			Map.Iterator<int, FilePackage> iters = osFile.iterator();
            
            while (iters.hasNext()){
                FilePackage fp = iters.getValue();
                if (fp != nilptr){
                    JsonArray files = fp.save();
                    object.put("" + iters.getKey(), files);
                }
                iters.next();
            }     
            
            return object;
        }
        
        public bool load(@NotNilptr JsonObject objects){
			bool bsucc = true;
            
			JsonArray fileo = (JsonArray)objects.child();
            while (fileo != nilptr){
				String arcname = fileo.getName();
                int fid = arcname.parseInt();
                try{
					FilePackage ar = osFile.get(fid);
                    if (ar != nilptr){
                        for (int i = 0, c = fileo.length(); i < c; i++){
                            ar.load(fileo.getString(i));
                        }
                    }
                }catch(Exception e){
					bsucc = false;
				}
                fileo = (JsonArray)fileo.next();
            }
            return bsucc;
        }
    };
    
    public class Package{
		public String lixpath = "";
        public String readme = "";
        public String icon = "";
        public String comment = "";
        
		public Map<int, Arch> archs = new Map<int, Arch>();
        
        public Package(){
			init();
        }
        
        public void init(){
			for (int i =0; i < 5; i++){
				archs.put(i , new Arch(i));
            }
        }
        public void reset(){
			archs.clear();
            init();
		}
        public void setComment(String text){
			comment = text;
        }
        
        public void preview(@NotNilptr QTreeWidget tree, long item){
			long hroot = tree.insertItem(item, "res/toolbar/staticfunction.png", lixpath);
            tree.insertItem(hroot, "res/toolbar/source.png", readme);
            
            Map.Iterator<int, Arch> iters = archs.iterator();
            
            while (iters.hasNext()){
                Arch ar = iters.getValue();
                if (ar != nilptr){
                    ar.preview(tree, hroot);
                }
				iters.next();
            }
        }
        
        public bool build(@NotNilptr ZipArchive zs){
            if (lixpath.length() > 0){
                String ext = lixpath.findExtension();
                
                bool isxl = false;
                if (ext.upper().equals(".XL")){
                    isxl = true;
                }
				if (false == zs.createEntry(new ZipFile(isxl ? "major.xl" : "major.lix", new FileInputStream(lixpath), false, _system_.currentTimeMillis()))){
					QMessageBox.Critical("错误", "添加文件 " + lixpath + " 时发生错误", QMessageBox.Ok, QMessageBox.Ok);
					return false;
                }
            }
            
            if (readme.length() > 0){
				if (false == zs.createEntry(new ZipFile("readme.md", new FileInputStream(readme), false, _system_.currentTimeMillis()))){
					QMessageBox.Critical("错误", "添加文件 " + readme + " 时发生错误", QMessageBox.Ok, QMessageBox.Ok);
					return false;
                }
            }
            
            if (icon.length() > 0){
				if (false == zs.createEntry(new ZipFile("icon.png", new FileInputStream(icon), false, _system_.currentTimeMillis()))){
					QMessageBox.Critical("错误", "添加文件 " + icon + " 时发生错误", QMessageBox.Ok, QMessageBox.Ok);
					return false;
                }
            }
            
            Map.Iterator<int, Arch> iters = archs.iterator();
            

            while (iters.hasNext()){
                Arch ach = iters.getValue();
                
                if (ach != nilptr){
                    if (false == ach.build(zs, "")){
                        return false;
                    }
                    prgExport.setValue((ach.id + 1) * 20);
                }
				iters.next();
            } 
            return true;
        }
        
        public JsonObject save(){
			JsonObject root = new JsonObject();
            root.put("lib",lixpath);
            root.put("md",readme);
            root.put("icon",icon);
            root.put("comment",comment);
            
            root.put("archs", saveArchs());
			return root;
        }
        
        public JsonObject saveArchs(){
			JsonObject archobj = new JsonObject();
            
            Map.Iterator<int, Arch> iters = archs.iterator();
            
            while (iters.hasNext()){
                Arch ar = iters.getValue();
                if (ar != nilptr){
                    JsonObject archFiles = ar.save();
                    archobj.put("" + iters.getKey(), archFiles);
                }
				iters.next();
            }
            return archobj;
        }
        
        public bool load(@NotNilptr JsonObject root ){
			try{
				lixpath = root.getString("lib");
                readme = root.getString("md");
                icon = root.getString("icon");
                comment = root.getString("comment");
                
                return loadArchs((JsonObject)root.get("archs"));
            }catch(Exception e){
				
            }
            return false;
        }
        
        public bool loadArchs(@NotNilptr JsonObject objects){
			bool bsucc = true;
			JsonObject archo = (JsonObject)objects.child();
            while (archo != nilptr){
				String arcname = archo.getName();
                if (arcname != nilptr){
                    int id = arcname.parseInt();
                    try{
                        Arch ar = archs.get(id);
                        if (ar != nilptr){
                            ar.load(archo);
                        }
                    }catch(Exception e){
                        bsucc = false;
                    }
                }
                archo = (JsonObject)archo.next();
                
            }
            return bsucc;
        }
    };
    
    Package currentPackage = new Package();
    
    public class BrowserListener: onClickListener{
		QLineEdit edit;
        String filter , title;
        bool save;
        
		public BrowserListener(QLineEdit _edit, bool _save, String _title, String _filter){
			edit = _edit;
            save = _save;
            filter = _filter;
            title = _title;
        }
        
		void onClick(QObject,  bool checked)override{
			String file ;
			if (save == false){
				file = QFileDialog.getOpenFileName(title, nilptr,  filter, ExportTool.this);
			}else{
				file = QFileDialog.getSaveFileName(title, nilptr,  filter, ExportTool.this);
            }
            
			if (file != nilptr && file.length() > 0){
				edit.setText(file);
			}
		}
    };
    
	public void onAttach()override{
    
		setFixedSize(width(), height());
        
		edtStaticLib = (QLineEdit)attachByName(new QLineEdit(), "edtStaticLib");
        
        destfile = (QLineEdit)attachByName(new QLineEdit(), "destfile");
        
        edtName = (QLineEdit)attachByName(new QLineEdit(), "edtName");
        edtVersion = (QLineEdit)attachByName(new QLineEdit(), "edtVersion");
        lelDescr = (QLineEdit)attachByName(new QLineEdit(), "lelDescr");
        lblAuthor = (QLineEdit)attachByName(new QLineEdit(), "lblAuthor");
        
        cbbNative = (QComboBox)attachByName(new QComboBox(), "cbbNative");
        cbbOs = (QComboBox)attachByName(new QComboBox(), "cbbOs");
        btnsave = (QPushButton)attachByName(new QPushButton(), "btnsave");
        btnload = (QPushButton)attachByName(new QPushButton(), "btnload");
        btnIcon = (QPushButton)attachByName(new QPushButton(), "btnIcon");
        stBrowser = (QPushButton)attachByName(new QPushButton(), "stBrowser");
        
        btnEmpty = (QPushButton)attachByName(new QPushButton(), "btnEmpty");
        btnAdd = (QPushButton)attachByName(new QPushButton(), "btnAdd");
        brwExport = (QPushButton)attachByName(new QPushButton(), "brwExport");
        btnExport = (QPushButton)attachByName(new QPushButton(), "btnExport");
        btnReadme = (QPushButton)attachByName(new QPushButton(), "btnReadme");
        structView = (QTreeWidget)attachByName(new QTreeWidget(), "structView");
        
        
        btnReadme.setOnMouseEventListener(new onMouseEventListener(){
        
			void onMouseMove(QObject obj,int Button,int x,int y,int flags,int source)override{
				String md = currentPackage.readme;
                
                QPoint cpt = btnReadme.mapToGlobal(x,y);
                if (md != nilptr && md.length() > 0){
					btnReadme.showToolTips(cpt.x,cpt.y,md, -1);
                }else{
					btnReadme.showToolTips(cpt.x,cpt.y,"没有添加README文件", -1);
                }
			}
        });
        prgExport = (QProgressBar)attachByName(new QProgressBar(), "prgExport");
        
        stBrowser.setOnClickListener(new BrowserListener(edtStaticLib, false, "选择链接库", "X 链接库 (*.lix *.xl)"));
        
        
		brwExport.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				String file = QFileDialog.getSaveFileName("导出文件", nilptr,  "X 包 (*.xp)", ExportTool.this);
				if (file != nilptr && file.length() > 0){
					String ext = file.findExtension();
                    
					if (ext.equalsIgnoreCase(".xp") == false){
						file = file + ".xp";
					}
					
					destfile.setText(file);
				}
            }                
        });

		btnsave.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				String file = QFileDialog.getSaveFileName("保存", nilptr,  "X导出项目 (*.exp)", ExportTool.this);
				if (file != nilptr && file.length() > 0){
					currentPackage.lixpath = edtStaticLib.getText();
					JsonObject root = currentPackage.save();
                    
                    if (root != nilptr){
						root.put("package", edtName.getText());
						root.put("version", edtVersion.getText());
						root.put("descr", lelDescr.getText());
						root.put("author", lblAuthor.getText());
                        
                        String content = root.toString(true);
                        
                        try{
                            FileOutputStream fos = new FileOutputStream(file);
                            byte [] data = content.getBytes();
                            fos.write(data, 0, data.length);
                            fos.close();
                        }catch(Exception e){
                            QMessageBox.Critical("错误","未能保存",QMessageBox.Ok,QMessageBox.Ok);
                        }
                    }
				}
            }
        });
        
        btnload.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				String file = QFileDialog.getOpenFileName("载入", nilptr,  "X导出项目 (*.exp)", ExportTool.this);
				if (file != nilptr && file.length() > 0){
					try{
						FileInputStream fis = new FileInputStream(file);
						byte [] data = fis.read();
						fis.close();
						String content = new String(data);
						JsonObject root = new JsonObject(content);
                        currentPackage.reset();
                        
                        if (false == currentPackage.load(root)){
							QMessageBox.Critical("注意","载入项目时有错误发生.",QMessageBox.Ok,QMessageBox.Ok);
                        }
                        
                        String packagename = root.getString("package");
                        String version = root.getString("version");
                        String descr = root.getString("descr");
                        String author = root.getString("author");
                        
                        if (packagename != nilptr){
							edtName.setText(packagename);
                        }
                        
                        if (version != nilptr){
							edtVersion.setText(version);
                        }
                        
                        if (descr != nilptr){
							lelDescr.setText(descr);
                        }
                        
                        if (author != nilptr){
							lblAuthor.setText(author);
                        }
                        if (currentPackage.icon != nilptr){
							btnIcon.setStyleSheetString("#btnIcon{border-image:url(\"" + currentPackage.icon + "\");}");
                        }else{
							btnIcon.setStyleSheetString("#btnIcon{border-image:url(\"" + "\");}");
                        }
                        
                        if (currentPackage.lixpath != nilptr){
							edtStaticLib.setText(currentPackage.lixpath);
                        }else{
							edtStaticLib.setText("");
                        }
                        
                        updatepreview();
					}catch(Exception e){
						QMessageBox.Critical("错误","未能载入项目",QMessageBox.Ok,QMessageBox.Ok);
					}
				}
            }
        });
        
        btnIcon.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				String file = QFileDialog.getOpenFileName("添加图标", nilptr,  "PNG图像 (*.png)", ExportTool.this);
				if (file != nilptr && file.length() > 0){
					currentPackage.icon = file;
                    btnIcon.setStyleSheetString("#btnIcon{border-image:url(\"" + file + "\");}");
				}
            }
        });
        
        btnReadme.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				String file = QFileDialog.getOpenFileName("添加注释", nilptr,  "文本文件 (*.* *)", ExportTool.this);
				if (file != nilptr && file.length() > 0){
					currentPackage.readme = file;
				}
            }
        });
        
        String [] options = {"x86", "x86_64", "arm", "arm64", "mips"};
        cbbNative.addItems(options);
        
        String [] oss = {"Windows", "Linux", "Macos", "其他"};
        cbbOs.addItems(oss);
        
        btnAdd.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
                String [] files  = QFileDialog.getOpenFileNames("添加运行时文件", nilptr,  "所有文件 (*.* *)", ExportTool.this);
				if (files != nilptr && files.length > 0){
                    for (int i = 0; i < files.length; i++){
                        addNativeFile(files[i]);
                    }
                }
            }
        });
        
        btnEmpty.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				int arch = cbbNative.getCurrentIndex() + 1;
                int os = cbbOs.getCurrentIndex();
                
                try{
                    Arch ar = currentPackage.archs.get(arch);
                    if (ar != nilptr){
                        FilePackage fp = ar.getOsFile().get(os);
                        if (fp != nilptr){
                            fp.files.clear();
                        }
                    }
                    updatepreview();
                }catch(Exception e){
					QMessageBox.Critical("错误", "添加文件时发生错误", QMessageBox.Ok, QMessageBox.Ok);
                }
            }
        });
        
        
        btnExport.setOnClickListener(new onClickListener(){
			void onClick(QObject obj, bool checked)override{
				onBuild();
            }
        });
        
        prgExport.setValue(0);
        
        String []columns = {"包内容"};
        structView.setColumns(columns);
        
        structView.setColumnWidth(0, 300);
        setWindowIcon("./res/toolbar/wizard.png");
    }

    public void addNativeFile(String add_path){
        int arch = cbbNative.getCurrentIndex() + 1;
        int os = cbbOs.getCurrentIndex();
        
        try{
            Arch a_rch = currentPackage.archs.get(arch);
            if (a_rch != nilptr){
                FilePackage fp = a_rch.getOsFile().get(os);
                
                if (fp != nilptr){
                    Vector<String> file = fp.files;
                    
                    if (file != nilptr){
                        for (int i = 0, c = file.size(); i < c; i++){
                            String fi = file.get(i);
                            if (fi != nilptr && fi.equals(add_path)){
                                return ;
                            }
                        }
                        file.add(add_path);
                    }
                }
            }
            updatepreview();
        }catch(Exception e){
            QMessageBox.Critical("错误", "添加文件时发生错误", QMessageBox.Ok, QMessageBox.Ok);
        }
    }
    
	public void updatepreview(){
        __nilptr_safe(structView);
		structView.clear();
        
        long root = structView.addItem("res/toolbar/package.png", "Package");
        currentPackage.lixpath = edtStaticLib.getText();
        currentPackage.preview(structView, root);
    }
    
    
    public void onBuild(){
		String outpath = destfile.getText();
        String descrtxt = lelDescr.getText();
        String author = lblAuthor.getText();
        
        currentPackage.lixpath = edtStaticLib.getText();
        
        String libname = edtName.getText();
        
        if (descrtxt.length() == 0){
			QMessageBox.Critical("错误", "没有填写简要描述", QMessageBox.Ok, QMessageBox.Ok);
			return ;
        }
        
        if (libname.length() == 0){
			QMessageBox.Critical("错误", "没有填写包名", QMessageBox.Ok, QMessageBox.Ok);
			return ;
        }
        
        if (Pattern.test(libname, "^[A-Za-z0-9_\.]+$", Pattern.NOTEMPTY, true) == false){
			QMessageBox.Critical("错误", "包名不合法", QMessageBox.Ok, QMessageBox.Ok);
			return ;
		}
        
        String version = edtVersion.getText();
                                    
        if (currentPackage.lixpath.length() == 0){
			QMessageBox.Critical("注意", "没有选择静态库文件", 	QMessageBox.Ok, QMessageBox.Ok);
            return ;
        }
        

        if (outpath.length() == 0){
			QMessageBox.Critical("注意", "没有选择输出文件", 	QMessageBox.Ok, QMessageBox.Ok);
            return ;
        }
        
        try{
			FileOutputStream fos = new FileOutputStream(outpath);
			
			ZipArchive xp = new ZipArchive();
			
            JsonObject root = new JsonObject();
            
            root.put("package", libname);
            root.put("version", version);
            root.put("descr", descrtxt);
            root.put("author", author);
            String comment = root.toString(true);
            currentPackage.setComment(comment);
            xp.setComment(comment);
			if (xp.create(fos)){
				if (currentPackage.build(xp)){
					xp.close();
					QMessageBox.Information("成功", "已成功导出:" + outpath, 	QMessageBox.Ok, QMessageBox.Ok);
                    close();
				}else{
					xp.close();
                    QMessageBox.Critical("错误", "导出文件时发生错误", QMessageBox.Ok, QMessageBox.Ok);
                }
               
			}
        }catch(Exception e){
            String msg = e.getMessage();
            if (msg == nilptr){
                msg = "unknow";
            }
			QMessageBox.Critical("注意", "未能导出:" + msg, 	QMessageBox.Ok, QMessageBox.Ok);
		}
    }
    
	public static bool Show(){
    
        QDialog newDlg = new QDialog();
        
        if (newDlg.load(UIManager.getUIData(__xPackageResource("ui/exportxp.ui"))) == false){
            return false;
        }
        
        ExportTool exportTool = new ExportTool();
        
        exportTool.attach(newDlg);
        exportTool.setModal(true);
        exportTool.show();
        return true;
    }
};