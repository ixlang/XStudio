//xlang Source, Name:XIndex.xcsm
//Date: Wed Sep 21:21:54 2018

class XIndexWindow : QMdiSubWindow
{
    public QWidget indexPage;
    QLabel [] labels = new QLabel[10];
    QLabel lblnew, lblopen, verslabel;
    QLabel lblhelp, lblrule;
    

    Object state;
    Recently.RecentlyObject [] objects = new Recently.RecentlyObject[10];
    public bool create( @NotNilptr QWidget parent)
    {
        if (super.create(parent)) {
            indexPage = new QWidget();
            if (indexPage.load(UIManager.getUIData(__xPackageResource("ui/index.ui"))) == false) {
                return false;
            }

            if (_system_.getPlatformId() == _system_.PLATFORM_MACOSX) {
                //qt在macos下有widget背景和别的平台不一致的情况，所以这里手动设置一下风格
                String wqss = "#Form{background-color:#3e3e42;}";
                if (Setting.isDarkStyle() == false) {
                    wqss = "#Form{background-color:#f0f0f0;}";
                }
                indexPage.setStyleSheetString(wqss);
            }
            setWidget(indexPage);
            indexPage.show();
            setMaximized(true);
            verslabel = (QLabel)indexPage.findByName("label");

            lblrule = (QLabel)indexPage.findByName("label_22");
            lblhelp = (QLabel)indexPage.findByName("label_17");

            lblnew = (QLabel)indexPage.findByName("lblnew");
            lblopen = (QLabel)indexPage.findByName("lblopen");

            verslabel.setText("XStudio " + Utils.getAppMajorVersion());
            for (int i =0; i < 10; i ++) {
                labels[i] = (QLabel)indexPage.findByName("rl" + i);
                labels[i].setOnMouseEventListener(recentlistener);
            }

            loadRecent();
            state = XWorkspace.hideDocks();

            lblhelp.setOnMouseEventListener(new onMouseEventListener() {
                void onMouseButtonRelease(QObject obj, int Button, int x, int y, int flags, int source) {
                    if (Button == 1) {
                        String url = _system_.getAppPath().findVolumePath().appendPath("./document/html/index.html");
                        QCore.openLocal(String.formatPath(url, false));
                    }
                }
            });

            lblrule.setOnMouseEventListener(new onMouseEventListener() {
                void onMouseButtonRelease(QObject obj, int Button, int x, int y, int flags, int source) {
                    if (Button == 1) {
                        String url = _system_.getAppPath().findVolumePath().appendPath("./document/xrule.html");
                        QCore.openLocal(String.formatPath(url, false));
                    }
                }
            });

            lblnew.setOnMouseEventListener(new onMouseEventListener() {
                void onMouseButtonRelease(QObject obj, int Button, int x, int y, int flags, int source) {
                    if (Button == 1) {
                        XWorkspace.workspace.createProject();
                    }
                }
            });

            lblopen.setOnMouseEventListener(new onMouseEventListener() {
                void onMouseButtonRelease(QObject obj, int Button, int x, int y, int flags, int source) {
                    if (Button == 1) {
                        XWorkspace.workspace.doOpen();
                    }
                }
            });
            XStudioApp._theApp.reconfig();
            return true;
        }
        return false;
    }



    onMouseEventListener recentlistener =  new onMouseEventListener()
    {
        void onMouseButtonRelease(QObject obj, int Button, int x, int y, int flags, int source) {
            if (Button == 1) {
                for (int i = 0; i < 10; i ++) {
                    if (labels[i] == obj) {
                        XWorkspace.workspace.loadProject(objects[i].path);
                        break;
                    }
                }
            }
        }
    };


    public bool onClose()override
    {
        XWorkspace.workspace.indexPage = nilptr;
        restore();
        XStudioApp._theApp.reconfig();
        return true;
    }

    public void restore()
    {
        if (state != nilptr) {
            XWorkspace.showDocks(state);
        }
    }

    public void loadRecent()
    {
        int c = Recently.count();
        int rl = 0;
        if (c > 10) {
            c = 10;
        }
        for (int i = c - 1; i >= 0; i --) {
            objects[rl] = Recently.getItem(i);
            String proj_file_path = String.formatPath(objects[rl].path,false);
            labels[rl].setText("<a style=\"color:#1684fb\">" + objects[rl].name + "</a> <small><em style=\"color:#fb8416\">" + objects[rl].date + "</small></em> <br /><a style=\" font-size:11px;text-overflow: ellipsis;overflow: hidden;\">" + proj_file_path + "</a>");
            labels[rl].setToolTips(proj_file_path);
            rl++;
        }

        while (rl < 10) {
            labels[rl++].hide();
        }
    }
};