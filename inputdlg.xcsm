//xlang Source, Name:gotoline.xcsm 
//Date: Sun Sep 19:06:03 2018 

class InputDialog : QDialog {

	public static class onInputListener{
		public bool onInputOk(@NotNilptr String text);
        public bool onInputCancel();
        public bool onSelectOk(int id){
            return false;
        }
        public String getTitle();
        public String getTips();
        public String getDefault();
    };
    
    public onInputListener listener;
	public InputDialog(onInputListener intputlis){
		listener = intputlis;
    }

    QPushButton btnOk, btnCancel;
    QLineEdit edtLine;
    
    String result = nilptr;
    public void onAttach()override{ 
    
        setWindowFlags(Constant.CustomizeWindowHint | Constant.WindowCloseButtonHint);
        
        setFixedSize(width(), height());
        
        btnOk = (QPushButton)attachByName(new QPushButton(), "btnOk");
        btnCancel = (QPushButton)attachByName(new QPushButton(), "btnCancel");
        
        edtLine = (QLineEdit)attachByName(new QLineEdit(), "edtLine");
        QLabel label = (QLabel)attachByName(new QLabel(), "label");
        
        if (label != nilptr){
            edtLine.setText(listener.getDefault());
            
            btnOk.setOnClickListener(
            new onClickListener(){
                void onClick(QObject obj, bool checked)override{
                    String text = edtLine.getText();
                    if (listener.onInputOk(text)){
                        result = text;
                        close();
                    }           
                }
            });
            
            btnCancel.setOnClickListener(
            new onClickListener(){
                void onClick(QObject obj, bool checked)override{
                    if (listener.onInputCancel()){
                        close();
                    }
                }
            });
            
            label.setText(listener.getTips());
            
            setWindowTitle(listener.getTitle());
            
            setModal(true);
            
            edtLine.setFocus();
        }
    }
    
    public static void requestInput(onInputListener lis){
		QDialog newDlg = new QDialog();
        if (newDlg.load(UIManager.getUIData(__xPackageResource("ui/goto.ui"))) == false){
            return ;
        }
        InputDialog wizard = new InputDialog(lis);	
        
        wizard.attach(newDlg);
        wizard.show();
    }
    
    public static String requestInputDlg(onInputListener lis){
		QDialog newDlg = new QDialog();
        if (newDlg.load(UIManager.getUIData(__xPackageResource("ui/goto.ui"))) == false){
            return nilptr;
        }
        InputDialog wizard = new InputDialog(lis);	
        
        wizard.attach(newDlg);
        wizard.exec();
        return wizard.result;
    }
};