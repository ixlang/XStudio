//xlang Source, Name:DeviceListDialog.xcsm 
//Date: Mon Dec 19:27:20 2019 

class DeviceListDialog : QDialog{
    QPushButton btnref, btndef, btnclose;
    QCheckBox chklogcat;
    QComboBox cbbloglvl;
    QTreeWidget list;
    Device [] devices;
    long defitem = 0;
    Thread refthread;
    
    DeviceListDialog(QDialog d){
        attach (d);
    }
    
    public static void create(QDialog d){
        DeviceListDialog ad = new DeviceListDialog(d);
        ad.show();
    }
    

    
    void onAttach(){
        setWindowIcon(Resources.thumb_png);

        chklogcat = (QCheckBox)attachByName(new QCheckBox(), "chklogcat");
        cbbloglvl = (QComboBox)attachByName(new QComboBox(), "cbbloglvl");
        
        btnref = (QPushButton)attachByName(new QPushButton(), "btnref");
        btndef = (QPushButton)attachByName(new QPushButton(), "btndef");
        btnclose = (QPushButton)attachByName(new QPushButton(), "btnclose");
        
        list = (QTreeWidget)attachByName(new QTreeWidget(), "devlist");
        
        String [] columns = {"设备", "序号", "类型"};
        
        list.setColumns(columns);
        
        list.setColumnWidth(0, 200);
        list.setColumnWidth(1, 200);
        list.setColumnWidth(2, 100);
        
        String[] lvlitems = {"详细", "调试", "信息", "警告", "错误", "致命"};
            
        cbbloglvl.addItems(lvlitems);
        cbbloglvl.setCurrentIndex(LogcatCacher.logcat_level);
        chklogcat.setOnClickListener(
        new onClickListener() {
            void onClick(QObject obj, bool checked)override {
                XAndroid.setLogcatEnable(checked);
            }
        });
        
        btnref.setOnClickListener(
        new onClickListener() {
            void onClick(QObject obj, bool checked)override {
                refreshDevice();
            }
        });
        
        btndef.setOnClickListener(
        new onClickListener() {
            void onClick(QObject obj, bool checked)override {
                long item = list.getSelItem();
                if (item != 0){
                    String defdev = list.getItemText(item, 1);
                    if (defitem != 0){
                        list.setItemBackColor(defitem, 0, 0);
                    }
                    list.setItemBackColor(item, 0, 0xff7fe22a);
                    defitem = item;
                    XAndroid.setDefaultDevice(defdev);
                }else{
                    QMessageBox.Critical("注意","选择的设备无效",QMessageBox.Ok,QMessageBox.Ok);
                }
            }
        });
        
        btnclose.setOnClickListener(
        new onClickListener() {
            void onClick(QObject obj, bool checked)override {
                close();
            }
        });
        
        cbbloglvl.setOnComboBoxEventListener(
            new onComboBoxEventListener() {
                void onItemSelected(QObject obj, int id) {
                    XAndroid.setLogcatLevel(id);
                }
            }
        );
        
        chklogcat.setCheck(XAndroid.LogcatEnabled());
        
        setModal(true);
        show();
        refreshDevice();
        
    }
    bool onClose()override {
       synchronized(DeviceListDialog.this){
            if (refthread != nilptr){
                return false;
            }
            return true;
       }
    }
    void refreshDevice(){
        synchronized(this){
            if (refthread == nilptr){ 
                list.clear();
                defitem = 0;
                btnref.setText("正在加载");
                btnref.setEnabled(false);
                new Thread(){
                    void run(){
                        devices = XAndroid.listDevice();
                        XAndroid.workspace.RunOnUi(new UIRunnable(){
                           void run() override{
                               
                               if (devices != nilptr){
                                   for (int i =0; i < devices.length; i++){
                                        long item = list.addItem(Resources.thumb_png,devices[i].model);
                                        
                                        if (XAndroid.defaultDevice != nilptr){
                                            if (XAndroid.defaultDevice.equals(devices[i].serial)){
                                                list.setItemBackColor(item, 0, 0xff7fe22a);
                                                defitem = item;
                                            }
                                        }
                                        
                                        list.setItemText(item, 1, devices[i].serial);
                                        list.setItemText(item, 2, devices[i].type);
                                   }
                               }
                               synchronized(DeviceListDialog.this){
                                    refthread = nilptr;
                               }
                               if (btnref.isValid()){
                                btnref.setText("刷新");
                                btnref.setEnabled(true);
                               }
                           }
                        });
                    }
                }.start();
            }
        }
    }
};