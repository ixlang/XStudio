//xlang Source, Name:FilterEdit.xcsm 
//Date: Fri Sep 12:25:16 2018 
class FilterEdit : QDialog {

	public FilterEdit(){
    
    }
    public FilterEdit(XLogcatWnd.Filter filter){
		modifyFilter = filter;
    }
    
    QPushButton btnAdd;
    QPushButton benCancel;
    QLineEdit filterName, editThread, editTag, editMsg;
    QCheckBox chkThread, chkTag, chkMsg;
    XLogcatWnd.Filter modifyFilter;
    
    QCheckBox []chkLevel = new QCheckBox[8];
    
    String [] chkLvlNames = {"chkEmerg", "chkAlert", "chkCrit", "chkErr", "chkWarning", "chkNotice", "chkInfo", "chkDebug"};
    
    public void onAttach(){ 
    
        setWindowFlags(Constant.CustomizeWindowHint | Constant.WindowCloseButtonHint );
        
        setFixedSize(width(), height());
        
        btnAdd = (QPushButton)attachByName(new QPushButton(), "btnAdd");
        benCancel = (QPushButton)attachByName(new QPushButton(), "benCancel");
        filterName = (QLineEdit)attachByName(new QLineEdit(), "filterName");
        editThread = (QLineEdit)attachByName(new QLineEdit(), "editThread");
        editTag = (QLineEdit)attachByName(new QLineEdit(), "editTag");
        editMsg = (QLineEdit)attachByName(new QLineEdit(), "editMsg");
        chkThread = (QCheckBox)attachByName(new QCheckBox(), "chkThread");
        chkTag = (QCheckBox)attachByName(new QCheckBox(), "chkTag");
        chkMsg = (QCheckBox)attachByName(new QCheckBox(), "chkMsg");
        
        for (int i = 0; i < chkLvlNames.length; i++){
			chkLevel[i] = (QCheckBox)attachByName(new QCheckBox(), chkLvlNames[i]);
            
            if (modifyFilter != nilptr){
				int flags = 1 << i;
				if ((modifyFilter.level & flags) == flags){
					chkLevel[i].setCheck(true);
                }
            }else{
				chkLevel[i].setCheck(true);
            }
        }
        
        if (modifyFilter != nilptr){
			filterName.setEnabled(false);
            filterName.setText(modifyFilter.name);
        }
        
        chkThread.setOnClickListener(
        new onClickListener(){
            void onClick(QObject obj, bool checked)override{
				editThread.setEnabled(checked);
            }
        });
        chkTag.setOnClickListener(
        new onClickListener(){
            void onClick(QObject obj, bool checked)override{
				editTag.setEnabled(checked);
            }
        });
        chkMsg.setOnClickListener(
        new onClickListener(){
            void onClick(QObject obj, bool checked)override{
				editMsg.setEnabled(checked);
            }
        });
        
        if (modifyFilter != nilptr){
			if ((modifyFilter.type & XLogcatWnd.Filter.FILTER_THREAD) == XLogcatWnd.Filter.FILTER_THREAD){
				editThread.setEnabled(true);
                chkThread.setCheck(true);
                editThread.setText(modifyFilter.key[0]);
            }
            
			if ((modifyFilter.type & XLogcatWnd.Filter.FILTER_TAG) == XLogcatWnd.Filter.FILTER_TAG){
				editTag.setEnabled(true);
                chkTag.setCheck(true);
                editTag.setText(modifyFilter.key[1]);
            }
            
			if ((modifyFilter.type & XLogcatWnd.Filter.FILTER_MSG) == XLogcatWnd.Filter.FILTER_MSG){
				editMsg.setEnabled(true);
                chkMsg.setCheck(true);
                editMsg.setText(modifyFilter.key[2]);
            }
        }
        btnAdd.setOnClickListener(
        new onClickListener(){
            void onClick(QObject obj, bool checked)override{
                complete();
            }
        });
        
        benCancel.setOnClickListener(
        new onClickListener(){
            void onClick(QObject obj, bool checked)override{
				close();
            }
        });
        
        if (modifyFilter != nilptr){
			setWindowTitle("编辑过滤器");
        }else{
			setWindowTitle("新建过滤器");
        }
        
        setModal(true);
        show();
    }
    
    public void complete(){
		String []key = new String[3];
        
		int type = 0;
        if (chkThread.getCheck()){
			type |= XLogcatWnd.Filter.FILTER_THREAD;
            key[0] = editThread.getText();
            if (key[0].length() == 0){
				QMessageBox.Critical("提示", "无效的过滤器表达式", QMessageBox.Ok, QMessageBox.Ok);
				return ;
            }
        }
        if (chkTag.getCheck()){
			type |= XLogcatWnd.Filter.FILTER_TAG;
            key[1] = editTag.getText();
            if (key[0].length() == 0){
				QMessageBox.Critical("提示", "无效的过滤器表达式", QMessageBox.Ok, QMessageBox.Ok);
				return ;
            }
        }
        if (chkMsg.getCheck()){
			type |= XLogcatWnd.Filter.FILTER_MSG;
            key[2] = editMsg.getText();
            if (key[2].length() == 0){
				QMessageBox.Critical("提示", "无效的过滤器表达式", QMessageBox.Ok, QMessageBox.Ok);
				return ;
            }
        }
        
        if (type == 0){
			QMessageBox.Critical("提示", "无效过滤器, 至少需要设置一个过滤条件", QMessageBox.Ok, QMessageBox.Ok);
            return ;
        }
        
        XLogcatWnd.Filter filter = modifyFilter;
        if (filter == nilptr){
			filter = new XLogcatWnd.Filter ();
        }
        filter.type = type;
        filter.key = key;
        filter.level = 0;
        
         for (int i = 0; i < chkLvlNames.length; i++){
			if (chkLevel[i].getCheck()){
				filter.level |= (1 << i);
            }
        }
        
        if (modifyFilter == nilptr){
			String filname = filterName.getText();
            if (filter.name.length() == 0){
				QMessageBox.Critical("提示", "无效的过滤器名称", QMessageBox.Ok, QMessageBox.Ok);
				return ;
            }
            filter.name = filname;
			if (XLogcatWnd.addFilter(filter) == false){
				QMessageBox.Critical("提示", "过滤器已经存在", QMessageBox.Ok, QMessageBox.Ok);
				return ;
			}
        }
        
        close();
    }
};