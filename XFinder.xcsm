//xlang Source, Name:XFinder.xcsm 
//Date: Mon Sep 13:36:15 2018 


class XFinder{

	public static class FindItem{
		public String file;
		public int position;
        public int line, row;
        public int type;
        public int findlen;
        public String findres;
        
        public int method;	//0:poslocation 1:linerowlocation
        
        public FindItem(String _file, int p, int l, String s){
			file = _file;
			position = p;
            findlen = l;
            findres = s;
            method = 0;
        }
        
        public FindItem(String _file, int _l, int _r, int l, String s){
			file = _file;
            findlen = l;
            line = _l;
            row = _r;
            findres = s;
            method = 1;
        }
    };
    
    public static class FindResult : Vector<FindItem>{};
    
    public static String readFileContent(@NotNilptr String file){
		FileInputStream fis = nilptr;
		try{
			fis = new FileInputStream(file);
			byte [] data = fis.read();
			try{
				return new String(data);
			}catch(Exception e){
				
			}
		}catch(Exception e){
		
		}finally{
            if (fis != nilptr){
				fis.close();
            }
        }
        return nilptr;
    }
    
    public static bool writeFileContent(@NotNilptr String file,@NotNilptr  String content){
		FileOutputStream fis = nilptr;
		try{
			fis = new FileOutputStream(file);
			byte [] data = content.getBytes();
			fis.write(data);
            return true;
		}catch(Exception e){
		
		}finally{
			if (fis != nilptr){
				fis.close();
            }
        }
        return false;
    }
    
    public static String getFileContent(@NotNilptr String file){
		String content = XSourceEditor.getEditingContent(file);
        if (content == nilptr){
			content = readFileContent(file);
        }
        return content;
    }
    
	public static const int 
		SCOPE_CURRENT = 0,
		SCOPE_ALLOPENED = 1,
		SCOPE_PROJECT = 2;
        
	public static void find(@NotNilptr FindResult result, Project project, bool bcase, bool wholeWord, bool regex,@NotNilptr  String [] extsions, int scope,@NotNilptr  String findTxt, Pattern pattern){
		if (scope == SCOPE_PROJECT && project != nilptr){
			for (int i = 0, c = project.getSourceFileCount(); i < c; i++){
				String path = project.getSourcePath(i);
                if (path != nilptr){
                    findInFile(result, path, bcase, wholeWord, regex, extsions, findTxt, pattern);
                }
            }
        }else
		if (scope == SCOPE_ALLOPENED){
			Map.Iterator<String, XSourceEditor> iter = XSourceEditor.editorMgr.iterator();
            while (iter.hasNext()){
				String file = iter.getKey();
                if (file != nilptr){
                    findInFile(result, file, bcase, wholeWord, regex, extsions, findTxt, pattern);
                }
                iter.next();
            }
        }
    }
    
    public static bool testExtsion(@NotNilptr String file , @NotNilptr String [] extsions){
		for (int i = 0; i < extsions.length; i++){
			if (file.lower().endWith(extsions[i].lower())){
				return true;
			}
        }
        return false;
    }
    
    public static void findInFile(@NotNilptr FindResult result,@NotNilptr  String file, bool bcase, bool wholeWord, bool regex, String [] extsions,@NotNilptr  String findTxt, Pattern pattern){
		if (extsions != nilptr && testExtsion(file, extsions) == false){
			return ;
        }
        
        String fileContent = getFileContent(file);
        
        if (fileContent != nilptr){
			findInText(result,  file, fileContent,  bcase,  wholeWord,  regex, findTxt, pattern);
        }
    }
    
    
    public static void findInText(@NotNilptr FindResult result, String file, @NotNilptr String text, bool bcase, bool wholeWord, bool regex,@NotNilptr  String findTxt, Pattern pattern){
		if (regex ){
            if (pattern != nilptr){
                findRegex(result, file, text, pattern);
            }
        }else
        if (bcase){
			if (wholeWord){
				findTextWholeWord(result, file, text, text, findTxt);
            }else{
				findText(result, file, text, text, findTxt);
            }
        }else{
			if (wholeWord){
				findTextWholeWord(result, file, text, text.lower(), findTxt.lower());
            }else{
				findText(result, file, text, text.lower(), findTxt.lower());
            }
        }
    }
    
    public static void findRegex(@NotNilptr FindResult result,String file,@NotNilptr  String text,@NotNilptr  Pattern findTxt){
		Pattern.Result 
			rt = findTxt.match(text, Pattern.NOTEMPTY);
            
        for (int i = 0, c = rt.length(); i < c; i++){
            int sl = rt.get(i).start();
            int el = rt.get(i).end();
            result.add(new FindItem(file, sl, el - sl, text.substring(sl,el)));
        }
    }
    
    public static String cutString(@NotNilptr String text, int sl, int el){
		sl = Math.max(sl - 20, 0);
        el = Math.min(text.length() - 1, el + 20);
        String out = text.substring(sl, el + 1);
        out = out.replace("\t", " ");
        out = out.replace("\r", " ");
        out = out.replace("\n", " ");
        return "..." + out + "...";
    }
    
    public static bool isWordChar(char c){
		return !(c >=0 && c < 48);
    }
    
    public static void findText(@NotNilptr FindResult result,String file,@NotNilptr  String originalText ,@NotNilptr String text,@NotNilptr  String findTxt){
		int findpos = -1;
        int findlen = findTxt.length();
        
        while ((findpos = text.indexOf(findTxt, findpos + 1)) != -1){
            result.add(new FindItem(file, findpos, findlen, cutString(originalText, findpos, findpos + findlen - 1)));
        }
    }
    
    public static void findTextWholeWord(@NotNilptr FindResult result,String file,@NotNilptr  String originalText ,@NotNilptr String text,@NotNilptr  String findTxt){
		int findpos = -1;
        int findlen = findTxt.length();
        
        while ((findpos = text.indexOf(findTxt, findpos + 1)) != -1){
	
            if (findpos > 0){
				char s = text.charAt(findpos - 1);
                if (isWordChar(s)){
					continue;
                }
            }
            if ((findpos + findlen) < text.length()){
				char e = text.charAt(findpos + findlen);
                if (isWordChar(e)){
					continue;
                }
            }

            result.add(new FindItem(file, findpos, findTxt.length(), cutString(originalText, findpos, findpos + findTxt.length() - 1)));
        }
    }
    
    public static void replace(@NotNilptr FindResult result,@NotNilptr  String replaceStr){
		for (int i = result.size() - 1; i >= 0; i--){
			FindItem item = result.get(i);
            if (item != nilptr){
				replaceOnFile(item, replaceStr);
            }
        }
    }
    
    public static void replaceOnFile(@NotNilptr FindItem item,@NotNilptr  String replaceStr){
		XSourceEditor wnd = XSourceEditor.findDocumentWindow(nilptr, item.file, false);
        
        if (wnd != nilptr){
			wnd.replaceText(item.position, item.findlen, replaceStr);
        }else{
			String content = readFileContent(item.file);
            if (content != nilptr){
				try{
					content = content.replace(item.position, item.position + item.findlen, replaceStr);
					writeFileContent(item.file, content);
                }catch(Exception e){
                
                }
            }
        }
    }
};